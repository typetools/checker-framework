buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Create OSGI bundles
        classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.2.0"
    }
}

task copySources(type: Copy) {
    description 'Copy checker-qual source from other projects.'
    includeEmptyDirs = false
    doFirst {
        // Delete the directory in case a previously copied file should no longer be in checker-qual
        delete file('src/main/java')
    }

    from files('../checker/src/main/java', '../dataflow/src/main/java', '../framework/src/main/java')
    // Qualifiers
    include '**/org/checkerframework/**/qual/*.java'
    include '**/PurityUnqualified.java'    // TODO: Should we move this into a qual directory?
    // Utility classes
    // If you change this list, also update ../LICENSE.txt .
    include "**/FormatUtil.java"
    include "**/I18nFormatUtil.java"
    include "**/NullnessUtil.java"
    include "**/Opt.java"
    include "**/RegexUtil.java"
    include "**/SignednessUtil.java"
    include "**/SignednessUtilExtra.java"
    include "**/UnitsTools.java"

    // Make files read only.
    fileMode(0444)

    into file('src/main/java')
}

apply plugin: 'biz.aQute.bnd.builder'

jar {
    dependsOn copySources
    manifest {
        attributes('Export-Package': '*')
    }
}

compileJava {
    dependsOn copySources
}

clean {
    delete file('src/')
}

task deployArtifactsToLocalRepo(dependsOn: jar) {
    description "Deploys ${jar.archiveFileName.get()} to the local Maven repository"
    doLast {
        mvnDeployToLocalRepo(project, "${pomFiles}/checker-qual-pom.xml")
    }
}

task deployArtifactsToSonatype {
    description "Deploys ${jar.archiveFileName.get()} to the Sonatype repository"
    dependsOn ('jar', 'sourcesJar', 'javadocJar')
    doLast {
        mvnSignAndDeployMultipleToSonatype(project, "${pomFiles}/checker-qual-pom.xml")
    }
}

apply from: rootProject.file("gradle-mvn-push.gradle")

final checkerQualPom(publication) {
    sharedPublicationConfiguration(publication)
    publication.from components.java
    // Information that is in all pom files is configured in checker-framework/gradle-mvn-push.gradle.
    publication.pom {
        name = 'Checker Qual'
        description = 'Checker Qual is the set of annotations (qualifiers) and supporting classes used by' +
                ' the Checker Framework to type check Java source code. Please see artifact: org.checkerframework:checker'
        licenses {
            license {
                name = 'The MIT License'
                url = 'http://opensource.org/licenses/MIT'
                distribution = 'repo'
            }
        }
    }
}
publishing {
    publications {
        checkerQual(MavenPublication) {
            checkerQualPom it
        }
    }
}
