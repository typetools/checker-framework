plugins {
  id("java-library")
}

ext {
  annotatedJdkHome = "../../jdk"
}
sourceSets {
  main {
    resources {
      // Stub files, message.properties, etc.
      srcDirs += [
        "src/main/java",
        "${buildDir}/generated/resources"
      ]
    }
  }
  testannotations
  stubifier
}

sourcesJar {
  // The resources duplicate content from the src directory.
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


configurations {
  implementation.extendsFrom(stubifierImplementation)
  implementation.extendsFrom(annotatedGuava)
}

def stubifierJar = tasks.register("stubifierJar", Jar) {
  from(sourceSets.stubifier.output)
  archiveClassifier.set("stubifier")
}

dependencies {
  api(project(":javacutil"))
  api(project(":dataflow"))
  api("org.checkerframework:stubparser:3.27.1")
  stubifierImplementation("org.checkerframework:stubparser:3.27.1")
  // This should be implementation sourceSets.stubifier.output, but that cause a failure in shadowJar.
  // This work around is from here: https://github.com/GradleUp/shadow/issues/1606#issuecomment-3164731141
  implementation(files(stubifierJar))
  api(project(":annotation-file-utilities"))
  api(project(":checker-qual"))

  // External dependencies:
  // If you add an external dependency, you must shadow its packages.
  // See the comment in ../build.gradle in the shadowJar block.
  implementation("org.plumelib:hashmap-util:${hashmapUtilVersion}")
  implementation("org.plumelib:plume-util:${plumeUtilVersion}")
  implementation("org.plumelib:reflection-util:${reflectionUtilVersion}")
  implementation("io.github.classgraph:classgraph:4.8.184")

  testImplementation("junit:junit:${junitVersion}")
  testImplementation(project(":framework-test"))
  testImplementation(sourceSets.testannotations.output)

  // AutoValue support in Returns Receiver Checker
  testImplementation("com.google.auto.value:auto-value-annotations:${autoValueVersion}")
  testImplementation("com.google.auto.value:auto-value:${autoValueVersion}")

  // Lombok support in Returns Receiver Checker
  testImplementation("org.projectlombok:lombok:${lombokVersion}")
}

tasks.register("cloneTypetoolsJdk", CloneTask) {
  description = "Obtain or update the annotated JDK."
  url.set("https://github.com/typetools/jdk.git")
  directory.set(file(annotatedJdkHome))
  outputs.upToDateWhen { false }
}


tasks.register("copyAndMinimizeAnnotatedJdkFiles", JavaExec) {
  dependsOn(cloneTypetoolsJdk)
  def inputDir = "${annotatedJdkHome}/src"
  def outputDir = "${buildDir}/generated/resources/annotated-jdk/"
  description = "Copy annotated JDK files to ${outputDir}. Removes private and package-private methods, method bodies, comments, etc. from the annotated JDK"
  group = "Build"
  dependsOn(":framework:compileStubifierJava")
  // We need the next two dependencies because we run JavaStubifier using this
  // project's runtimeClasspath, which refers to the jars for these other projects.
  dependsOn(":javacutil:jar")
  dependsOn(":dataflow:jar")

  doFirst {
    FileTree tree = fileTree(dir: inputDir)
    NavigableSet<String> annotatedForFiles = new TreeSet<>()
    // Populate `annotatedForFiles`.
    tree.visit { FileVisitDetails fvd ->
      if (!fvd.file.isDirectory() && fvd.file.name.matches(".*\\.java")
          && !fvd.file.path.contains("org/checkerframework")) {
        fvd.getFile().readLines().any { line ->
          if (line.contains("@AnnotatedFor") || line.contains("org.checkerframework")) {
            annotatedForFiles.add(fvd.file.absolutePath)
            return true
          }
        }
      }
    }
    String absolutejdkHome = file(annotatedJdkHome).absolutePath
    int jdkDirStringSize = absolutejdkHome.size()
    copy {
      from(annotatedJdkHome)
      into(outputDir)
      for (String filename : annotatedForFiles) {
        include(filename.substring(jdkDirStringSize))
      }
    }
  }

  inputs.dir(file(inputDir))
  outputs.dir(file(outputDir))

  classpath = sourceSets.stubifier.runtimeClasspath
  standardOutput = System.out
  errorOutput = System.err

  mainClass = "org.checkerframework.framework.stubifier.JavaStubifier"
  args(outputDir)
}
sourcesJar.dependsOn(copyAndMinimizeAnnotatedJdkFiles)
processResources.dependsOn(copyAndMinimizeAnnotatedJdkFiles)

tasks.register("allSourcesJar", Jar) {
  description = "Creates a sources jar that includes sources for all Checker Framework classes in framework.jar"
  group = "Build"
  destinationDirectory = file("${projectDir}/dist")
  archiveFileName = "framework-source.jar"
  from(project(":framework").sourceSets.main.java,
      project(":dataflow").sourceSets.main.allJava,
      project(":javacutil").sourceSets.main.allJava)
}

tasks.register("allJavadocJar", Jar) {
  description = "Creates javadoc jar include Javadoc for all of the framework"
  group = "Build"
  dependsOn(project(":framework").tasks.javadoc,
      project(":dataflow").tasks.javadoc,
      project(":javacutil").tasks.javadoc)

  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  destinationDirectory = file("${projectDir}/dist")
  archiveFileName = "framework-javadoc.jar"
  from(project(":framework").tasks.javadoc.destinationDir,
      project(":dataflow").tasks.javadoc.destinationDir,
      project(":javacutil").tasks.javadoc.destinationDir)
}

shadowJar {
  description = "Creates the \"fat\" framework.jar in dist"
  destinationDirectory = file("${projectDir}/dist")
  archiveFileName = "framework.jar"
  manifest {
    attributes("Automatic-Module-Name": "org.checkerframework.framework")
  }
}

tasks.register("loaderTests", Exec) {
  dependsOn("shadowJar")
  description = "Run tests for the annotation class loader"
  group = "Verification"
  dependsOn(compileTestJava)
  // TODO: this dependency on checker is a bit ugly.
  dependsOn(project(":checker-qual").tasks.jar)
  dependsOn(project(":checker").tasks.assemble)
  commandLine("make", "-C", "tests/annotationclassloader/", "all")
}

clean {
  delete("tests/returnsreceiverdelomboked")
  delete("dist")
}


if (skipDelombok) {
  test {
    exclude("**/ReturnsReceiverLombokTest.java")
  }
} else {
  tasks.register("delombok") {
    description = "Delomboks the source code tree in tests/returnsreceiverlombok"

    def srcDelomboked = "tests/returnsreceiverdelomboked"
    def srcJava = "tests/returnsreceiverlombok"

    inputs.files(file(srcJava))
    outputs.dir(file(srcDelomboked))

    // This dependency is required to ensure the checker-qual jar exists,
    // to prevent lombok from emitting "cannot find symbol" errors for @This
    // annotations in the test input code.
    dependsOn(project(":checker-qual").tasks.jar)

    doLast {
      if (!skipDelombok) {
        def collection = files(configurations.testCompileClasspath)
        ant.taskdef(name: "delombok", classname: "lombok.delombok.ant.Tasks\$Delombok",
        classpath: collection.asPath)
        ant.delombok(from: srcJava, to: srcDelomboked, classpath: collection.asPath)
      }
    }
  }
  tasks.test.dependsOn("delombok")
}
