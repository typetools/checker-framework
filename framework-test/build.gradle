import org.gradle.internal.jvm.Jvm

sourceSets {
    taglet
    tagletJdk11
}

dependencies {
    // If this version changes, then the version
    // docs/developer/release/maven-artifacts/framework-test-pom.xml must be changed, too.
    implementation  group: 'junit', name: 'junit', version: '4.13.1'
    implementation project(':javacutil')
    implementation project(':checker-qual')

    implementation 'org.plumelib:plume-util:1.4.1'

    if (Jvm.current().toolsJar) {
        tagletImplementation files(Jvm.current().toolsJar)
    }
}

jar.archiveBaseName = 'framework-test'

task deployArtifactsToLocalRepo(dependsOn: jar) {
    description "Deploys ${jar.archiveFileName.get()} to the local Maven repository"
    doLast {
        mvnDeployToLocalRepo(project, "${pomFiles}/framework-test-pom.xml")
    }
}

task deployArtifactsToSonatype {
    description "Deploys ${jar.archiveFileName.get()} to the Sonatype repository"
    dependsOn ('jar', 'sourcesJar', 'javadocJar')
    doLast {
        mvnSignAndDeployMultipleToSonatype(project, "${pomFiles}/framework-test-pom.xml")
    }
}

apply from: rootProject.file("gradle-mvn-push.gradle")

final frameworkTest(publication) {
    publication.from components.java
    // Information that is in all pom files is configured in checker-framework/gradle-mvn-push.gradle.
    publication.pom {
        name = 'Checker Framework Testing Library'
        description = 'Framework-Test is a collection of utility classes for testing checkers that use the Checker Framework.'
        licenses {
            license {
                name = 'GNU General Public License, version 2 (GPL2), with the classpath exception'
                url = 'http://www.gnu.org/software/classpath/license.html'
                distribution = 'repo'
            }
        }
    }
}

publishing {
    publications {
        remote(MavenPublication) {
            frameworkTest it
        }

        local(MavenPublication) {
            frameworkTest it
        }
    }
}