import org.gradle.internal.jvm.Jvm

sourceSets {
    taglet
    tagletJdk11
}

dependencies {
    // If this version changes, then the version in maven-artifacts/poms/frameworktestPom.xml
    // and maven-artifacts/release/frameworktestReleasePom.xml must be changed, too.
    implementation  group: 'junit', name: 'junit', version: '4.13'
    implementation project(':javacutil')

    if (Jvm.current().toolsJar) {
        tagletImplementation files(Jvm.current().toolsJar)
    }
}

jar.archiveBaseName = 'framework-test'

task deployArtifactsToLocalRepo(dependsOn: jar) {
    description "Deploys ${jar.archiveFileName.get()} to the local Maven repository. "
    doLast {
        mvnDeployToLocalRepo(project, "${rootDir}/maven-artifacts/release/frameworktestReleasePom.xml".toString())
    }
}

task deployArtifactsToSonatype {
    description "Deploys ${jar.archiveFileName.get()} to the Sonatype repository"
    dependsOn ('jar', 'sourcesJar', 'javadocJar')
    doLast {
        mvnSignAndDeployMultipleToSonatype(project, "${rootDir}/maven-artifacts/release/frameworktestReleasePom.xml".toString())
    }
}