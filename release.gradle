import java.nio.file.Files
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

ext {
  currentDate = LocalDateTime.now().format(DateTimeFormatter.ofPattern("d MMM yyyy"))
  releaseVersionRegex = "\\d\\.\\d+\\.\\d+(?:\\.\\d)"
  releaseVersion = '3.49.6-SNAPSHOT'
}

tasks.register('updateVersionNumbers', Copy){
  dependsOn('updateVersionNumbersInDocs')
  dependsOn('updateVersionNumbersAFU')
  from layout.buildDirectory.file('updateVersionNumbers/')
  into layout.projectDirectory
}

tasks.register('updateVersionNumbersAFU', Copy){
  into layout.buildDirectory.file('updateVersionNumbers/annotation-file-utilities')
  from 'annotation-file-utilities'
  include 'src/main/java/org/checkerframework/afu/scenelib/io/classfile/ClassFileReader.java'
  filter { String line ->
    line.replaceAll( "Annotation File Utilities v${releaseVersionRegex}{0,1}", "Annotation File Utilities v${releaseVersion}")
  }
  doFirst {
    if(!file('annotation-file-utilities/src/main/java/org/checkerframework/afu/scenelib/io/classfile/ClassFileReader.java').exists()){
      throw new GradleException("File annotation-file-utilities/src/main/java/org/checkerframework/afu/scenelib/io/classfile/ClassFileReader.java not found.")
    }
  }
}

tasks.register('updateVersionNumbersInDocs', Copy) {
  into layout.buildDirectory.file('updateVersionNumbers/docs/')
  from('docs/') {
    include 'manual/annotation-file-utilities/annotation-file-utilities.html',
        'checker-framework-quick-start.html',
        'checker-framework-webpage.html',
        'manual/checker-framework/introduction.tex'

    filter { String line ->
      line = line.replaceAll("checker-framework-${releaseVersionRegex}{0,1}", "checker-framework-${releaseVersion}")
      line.replaceAll("<!-- checker-framework-date -->(.*)<!-- /checker-framework-date -->", "<!-- checker-framework-date -->${currentDate}<!-- /checker-framework-date -->")
    }
  }

  from('docs') {
    include 'manual/checker-framework/external-tools.tex'
    filter { String line ->
      line = line.replaceAll("checker-framework-${releaseVersionRegex}{0,1}", "checker-framework-${releaseVersion}");

      line = line.replaceAll("checker-${releaseVersionRegex}{0,1}", "checker-${releaseVersion}");
      line = line.replaceAll("checker-qual-${releaseVersionRegex}{0,1}", "checker-qual-${releaseVersion}");
      line = line.replaceAll("checker-util-${releaseVersionRegex}{0,1}", "checker-util-${releaseVersion}");

      line = line.replaceAll("checker/${releaseVersionRegex}{0,1}", "checker/${releaseVersion}");
      line = line.replaceAll("checker-qual/${releaseVersionRegex}{0,1}", "checker-qual/${releaseVersion}");
      line = line.replaceAll("checker-util/${releaseVersionRegex}{0,1}", "checker-util/${releaseVersion}");

      line = line.replaceAll("/${releaseVersionRegex}{0,1}/jar", "/${releaseVersion}/");

      line.replaceAll("ext.checkerFrameworkVersion = '${releaseVersionRegex}{0,1}", "ext.checkerFrameworkVersion = '${releaseVersion}");
    }
  }

  from('docs') {
    include 'manual/checker-framework/manual.tex'

    filter { String line ->
      line = line.replaceAll("ReleaseInfo\\}\\{(.*)\\}", "ReleaseInfo}{${releaseVersion} (${currentDate})}");
      line.replaceAll("ReleaseVersion\\}\\{(.*)\\}", "ReleaseVersion}{${releaseVersion}}");
    }
  }

  from('docs') {
    include 'examples/MavenExample/pom.xml'

    filter { String line ->
      line.replaceAll("<!-- checker-framework-version -->(.*)<!-- /checker-framework-version -->", "<!-- checker-framework-version -->${releaseVersion}<!-- /checker-framework-version -->")
    }
  }
}


tasks.register('createCheckerFrameworkZip', Zip) {
  description = 'Creates a zip archive for the Checker Framework.'
  dependsOn('buildAll')
  archiveFileName = "checker-framework-${version}.zip"
  destinationDirectory = file(project.property('destDir'))

  from(layout.projectDirectory) {
    exclude 'api/**',
        'checker/bin/README',
        'checker/bin/javac',
        'checker/dist/*.asc',
        'docs/tutorial/tests/**',
        'docs/tutorial/src/**',
        'docs/tutorial/test/**',
        'docs/tutorial/Makefile',
        'docs/tutorial/README'

    include 'README.html',
        'LICENSE.txt',
        'annotation-file-utilities/bin/**',
        'annotation-file-utilities/dist/**',
        'annotation-file-utilities/LICENSE.txt',
        'checker/bin/**',
        'checker/dist/**',
        'checker/resources/**',
        'docs/CHANGELOG.md',
        'docs/manual/annotation-file-utilities/annotation-file-utilities.html',
        'docs/manual/annotation-file-utilities/annotation-file-format.pdf',
        'docs/manual/annotation-file-utilities/annotation-file-format.html',
        'docs/manual/annotation-file-utilities/*.png',
        'docs/manual/checker-framework/manual.html',
        'docs/manual/checker-framework/manual.pdf',
        'docs/manual/checker-framework/*.svg',
        'docs/manual/checker-framework/manual001.png',
        'docs/manual/checker-framework/manual001.svg',
        'docs/examples/InterningExample.java',
        'docs/examples/InterningExampleWithWarnings.java',
        'docs/examples/NullnessExample.java',
        'docs/examples/NullnessExampleWithWarnings.java',
        'docs/examples/NullnessReleaseTests.java',
        'docs/examples/units-extension/Demo.java',
        'docs/examples/units-extension/Expected.txt',
        'docs/examples/units-extension/Frequency.java',
        'docs/examples/units-extension/FrequencyRelations.java',
        'docs/examples/units-extension/Hz.java',
        'docs/examples/units-extension/kHz.java',
        'docs/examples/units-extension/Makefile',
        'docs/examples/units-extension/README',
        'docs/examples/MavenExample/pom.xml',
        'docs/examples/MavenExample/README',
        'docs/examples/MavenExample/src/main/java/org/checkerframework/example/MavenExample.java',
        'docs/tutorial/**'
  }

  from('dataflow/manual/dataflow.pdf') {
    into 'docs/manual/checker-framework/'

    rename 'dataflow.pdf', 'checker-framework-dataflow-manual.pdf'
  }

  from('docs/logo/Logo/CFLogo.png') {
    into 'docs/manual/checker-framework/'
  }

  from('docs/logo/Logo/CFLogo.png') {
    into 'docs/tutorial/'
  }

  from('docs/logo/Logo/CFLogo.png') {
    into 'docs/tutorial/webpages'
  }
}

tasks.register('zipMavenExamples', Zip) {
  description = 'Creates a zip archive for the Maven example.'
  archiveFileName = "maven-examples-${version}.zip"
  destinationDirectory = file(project.property('destDir'))
  from('docs/examples/MavenExample')
}

tasks.register('copyWebRoot', Copy) {
  String webRoot = project.property('webRoot')
  destinationDir=file(webRoot)
  from ("${projectDir}/docs") {
    include('checker-framework-webpage.html', 'CHANGELOG.md')
  }
  from("${projectDir}/docs/logo/Logo/CFLogo.png")
  from("${projectDir}/docs/logo/Checkmark/CFCheckmark_favicon.png") {
    rename('CFCheckmark_favicon.png','favicon-checkerframework.png' )
  }

  doLast{
    Files.createSymbolicLink(file("${webRoot}/index.html").toPath(), file("${webRoot}/checker-framework-webpage.html").toPath())
  }
}

tasks.register('copyCFWebsite', Copy) {
  String destDir = project.property('cfWebsite')
  destinationDir = file(destDir)

  from('docs/logo/Logo/CFLogo.png') {
    into 'tutorial/'
  }

  from('docs/logo/Logo/CFLogo.png') {
    into 'tutorial/webpages'
  }

  from('docs/logo/Logo/CFLogo.png') {
    into 'manual'
  }

  // Copy favicon
  from('docs/logo/Checkmark/CFCheckmark_favicon.png') {
    rename('CFCheckmark_favicon.png','favicon-checkerframework.png' )
  }

  from('docs/logo/Checkmark/CFCheckmark_favicon.png') {
    into 'manual'
    rename('CFCheckmark_favicon.png','favicon-checkerframework.png' )
  }

  // Copy manual files
  from('docs/manual/checker-framework'){
    into 'manual'
    include 'manual.html',
        'manual.pdf',
        '*.svg',
        'manual001.png',
        'manual001.svg'
  }

  // Copy developer docs
  from('docs/developer'){
    into 'manual'
    include 'developer-manual.html',
        'gsoc-ideas.html',
        'new-contributor-projects.html'
  }

  // Copy Dataflow manual
  from ('dataflow/manual/dataflow.pdf'){
    into 'manual'
    rename 'dataflow.pdf','checker-framework-dataflow-manual.pdf'
  }

  // Copy tutorial
  from ('docs/tutorial'){
    into 'tutorial'
  }

  // Copy Javadoc
  from ('docs/api') {
    into 'api'
  }

  doLast{
    Files.createSymbolicLink(file("${destDir}/manual/index.html").toPath(), file("${deployDir}/manual.html").toPath())
  }
}

tasks.register('copyAFUWebsite', Copy) {
  description = 'Copy website deployDir.'
  String destDir = project.property('afuWebsite')
  destinationDir = file(destDir)

  from projectDir

  include 'annotation-file-utilities.html'
  include 'annotation-file-format.html'
  include 'annotation-file-format.pdf'
  include 'figures/*.svg'
  include 'figures/*.png'
  include 'figures/*.gif'

  doLast {

    delete "${destDir}/index.html"
    Files.createSymbolicLink(file("${deployDir}/index.html").toPath(), file("${deployDir}/annotation-file-utilities.html").toPath())
  }
}

tasks.register('updateCopyMavenExample', Copy){
  String destDir = project.property('destDir') + '/MavenExample'
  destinationDir = file(destDir)
  from 'docs/examples/MavenExample'
  filter { String line ->
    line.replaceAll("<!-- checker-framework-version -->(.*)<!-- /checker-framework-version -->", "<!-- checker-framework-version -->${releaseVersion}<!-- /checker-framework-version -->")
  }
}

tasks.register('copyToWebsite'){
  dependsOn('copyCFWebsite', 'copyWebRoot','copyAFUWebsite')
  doFirst {
    if(!(project.hasProperty('afuWebsite') && project.hasProperty('webRoot') && project.hasProperty('cfWebsite'))) {
      println project.properties.entrySet()*.toString().sort().toString().replaceAll(", ", "\n")
      throw new RuntimeException("You must specify the following properties: webRoot, afuWebsite, cfWebsite")
    }
  }
}
